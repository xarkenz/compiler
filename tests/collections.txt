function malloc(size_bytes: u64) -> *own void;
function free(alloc: *own void);
function printf(format: *[u8], ..) -> u32;

implement i32 {
    function max(self: Self, other: Self) -> Self {
        if (self > other) {
            return self;
        }
        else {
            return other;
        }
    }

    function cmp(self: *Self, other: *Self) -> i32 {
        if (*self < *other) {
            return -1;
        }
        else if (*self > *other) {
            return +1;
        }
        else {
            return 0;
        }
    }

    function print(self: *Self) {
        printf("%d", *self);
    }
}

struct LinkedListNode {
    value: *void,
    next: *own LinkedListNode,
}

struct LinkedList {
    head: *own LinkedListNode,
}

implement LinkedList {
    function new() -> Self {
        return LinkedList {
            head: null,
        };
    }

    function front(self: *Self) -> *void {
        if (self.head == null) {
            return null;
        }
        else {
            return self.head.value;
        }
    }

    function push_front(self: *mut Self, value: *void) {
        let mut new_node: *own LinkedListNode = malloc(sizeof(LinkedListNode));
        *new_node = LinkedListNode {
            value: value,
            next: self.head,
        };
        self.head = new_node;
    }

    function pop_front(self: *mut Self) -> *void {
        if (self.head == null) {
            return null;
        }
        else {
            let front: *own LinkedListNode = self.head;
            let value: *void = front.value;
            self.head = front.next;
            free(front);
            return value;
        }
    }
}

struct AVLTreeNode {
    key: *void,
    left: *own AVLTreeNode,
    right: *own AVLTreeNode,
    height: i32,
}

implement AVLTreeNode {
    function alloc(key: *void) -> *own Self {
        let mut alloc: *own Self = malloc(sizeof(Self));
        *alloc = AVLTreeNode {
            key: key,
            left: null,
            right: null,
            height: 0,
        };
        return alloc;
    }

    function get_height(self: *Self) -> i32 {
        // sneaky little trick
        if (self == null) {
            return -1;
        }
        else {
            return self.height;
        }
    }

    function recompute_height(self: *mut Self) {
        self.height = 1 + i32::max(
            self.left.get_height(),
            self.right.get_height(),
        );
    }

    function rotate_right(mut self: *own Self) -> *own Self {
        let mut new_root: *own Self = self.left;
        self.left = new_root.right;
        new_root.right = self;
        self.recompute_height();
        new_root.recompute_height();
        return new_root;
    }

    function rotate_left(mut self: *own Self) -> *own Self {
        let mut new_root: *own Self = self.right;
        self.right = new_root.left;
        new_root.left = self;
        self.recompute_height();
        new_root.recompute_height();
        return new_root;
    }

    function balance(mut self: *own Self) -> *own Self {
        if (self == null) {
            return null;
        }
        let imbalance: i32 = self.left.get_height() - self.right.get_height();
        if (imbalance > 1) {
            if (self.left.right.get_height() > self.left.left.get_height()) {
                self.left = (self.left as *own Self).rotate_left() as *own Self;
            }
            self = (self as *own Self).rotate_right() as *own Self;
        }
        else if (imbalance < -1) {
            if (self.right.left.get_height() > self.right.right.get_height()) {
                self.right = (self.right as *own Self).rotate_right() as *own Self;
            }
            self = (self as *own Self).rotate_left() as *own Self;
        }
        else {
            self.recompute_height();
        }
        return self;
    }

    function print(self: *Self, printer: function(*void)) {
        if (self == null) {
            printf("_");
        }
        else {
            printf("(");
            self.left.print(printer);
            printf(", ");
            self.right.print(printer);
            printf(")");
        }
    }
}

struct AVLTree {
    root: *own AVLTreeNode,
    comparator: function(*void, *void) -> i32,
}

implement AVLTree {
    function new(comparator: function(*void, *void) -> i32) -> Self {
        return AVLTree {
            root: null,
            comparator: comparator,
        };
    }

    function get(self: *Self, key: *void) -> *void {
        let mut node: *AVLTreeNode = self.root;
        while (node != null) {
            let ordering: i32 = self.comparator(key, node.key);
            if (ordering < 0) {
                node = node.left;
            }
            else if (ordering > 0) {
                node = node.right;
            }
            else {
                return node.key;
            }
        }
        return null;
    }

    function insert_subtree(self: *mut Self, node_ref: *mut *own AVLTreeNode, key: *void) -> *void {
        if (*node_ref == null) {
            *node_ref = AVLTreeNode::alloc(key);
            return null;
        }
        let ordering: i32 = self.comparator(key, (*node_ref).key);
        if (ordering < 0) {
            self.insert_subtree(&(*node_ref).left, key);
        }
        else if (ordering > 0) {
            self.insert_subtree(&(*node_ref).right, key);
        }
        else {
            let replaced_key: *void = (*node_ref).key;
            (*node_ref).key = key;
            return replaced_key;
        }
        *node_ref = (*node_ref as *own AVLTreeNode).balance() as *own AVLTreeNode;
        return null;
    }

    function insert(self: *mut Self, key: *void) -> *void {
        return self.insert_subtree(&self.root, key);
    }

    function print(self: *Self, printer: function(*void)) {
        self.root.print(printer);
        printf("\n");
    }
}

struct BTreeNode;

struct BTreeNodeKey {
    boundary: *void,
    left: *own BTreeNode,
}

struct BTreeNode {
    is_leaf: bool,
    degree: u64,
    keys: *own [BTreeNodeKey],
    right: *own BTreeNode,
}

struct BTreeLeaf {
    is_leaf: bool,
    degree: u64,
    elements: *own [*void],
}

implement BTreeLeaf {
    function alloc(l_order: u64, first_element: *void) -> *own Self {
        let mut elements: *own [*void] = malloc(sizeof(*void) * l_order);
        elements[0] = first_element;
        let mut alloc: *own Self = malloc(sizeof(Self));
        *alloc = Self {
            is_leaf: true,
            degree: 1,
            elements: elements,
        };
        return alloc;
    }
}

struct BTree {
    m_order: u64,
    l_order: u64,
    comparator: function(*void, *void) -> i32,
    root: *own BTreeNode,
}

implement BTree {
    function new(m_order: u64, l_order: u64, comparator: function(*void, *void) -> i32) -> Self {
        return BTree {
            m_order: m_order,
            l_order: l_order,
            comparator: comparator,
            root: null,
        };
    }

    function insert(self: *mut Self, key: *void) -> *void {
        if (self.root == null) {
            self.root = BTreeLeaf::alloc(self.l_order, key) as *own BTreeNode;
            return null;
        }
        return null;
    }
}

function max_percolate_down(array: *mut [*void], length: u64, comparator: function(*void, *void) -> i32, mut index: u64) {
    let target: *void = array[index];
    while (true) {
        let left: u64 = (index + 1) * 2 - 1;
        let right: u64 = left + 1;
        if (left >= length) {
            break;
        }
        let mut max: u64;
        if (right >= length) {
            max = left;
        }
        else if (comparator(array[left], array[right]) > 0) {
            max = left;
        }
        else {
            max = right;
        }
        if (comparator(array[max], target) > 0) {
            array[index] = array[max];
            index = max;
        }
        else {
            break;
        }
    }
    array[index] = target;
}

function heap_sort(array: *mut [*void], length: u64, comparator: function(*void, *void) -> i32) {
    // Build max heap
    let mut index: u64 = length / 2;
    while (index > 0) {
        index -= 1;
        // Percolate down
        max_percolate_down(array, length, comparator, index);
    }
    // Build sorted array
    index = length;
    while (index > 1) {
        index -= 1;
        // Swap max with last unsorted element
        let max_value: *void = array[0];
        array[0] = array[index];
        array[index] = max_value;
        // Percolate the root down within the unsorted range
        max_percolate_down(array, index, comparator, 0);
    }
}

function print_i32_ptr_array(array: *[*i32], length: u64) {
    if (length == 0) {
        printf("[]");
        return;
    }
    printf("[%d", *array[0]);
    let mut index: u64 = 1;
    while (index < length) {
        printf(", %d", *array[index]);
        index += 1;
    }
    printf("]");
}

function main() -> i32 {
    let keys: [i32; 15] = [1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15];

    let mut list: LinkedList = LinkedList::new();
    let mut i: u64 = 0;
    while (i < 5) {
        printf("push: %d\n", keys[i]);
        list.push_front(&keys[i]);
        i += 1;
    }
    let mut value: *i32;
    while ((value = list.pop_front()) != null) {
        printf("pop: %d\n", *value);
    }
    
    let mut tree: AVLTree = AVLTree::new(i32::cmp);
    let mut i: u64 = 0;
    while (i < 7) {
        let idx: u64 = (i * 7) % 10;
        printf("insert: %d\n", keys[idx]);
        tree.insert(&keys[idx]);
        i += 1;
    }
    let mut i: u64 = 0;
    while (i < 10) {
        let key: *i32 = tree.get(&keys[i]);
        let mut is_contained: *[u8];
        if (key != null) {
            is_contained = "yes";
        }
        else {
            is_contained = "no";
        }
        printf("contains %d: %s\n", keys[i], is_contained);
        i += 1;
    }
    tree.print(i32::print);

    let mut b_tree: BTree = BTree::new(3, 2, i32::cmp);
    b_tree.insert(&keys[0]);

    let mut heap_sort_test: [*i32; 15];
    let mut index: u64 = 0;
    while (index < 15) {
        let key: *i32 = &keys[((index + 7) * 7) % 15];
        heap_sort_test[index] = key;
        index += 1;
    }
    printf("unsorted: ");
    print_i32_ptr_array(&heap_sort_test, 15);
    printf("\n");
    heap_sort(&heap_sort_test, 15, i32::cmp);
    printf("heapsort: ");
    print_i32_ptr_array(&heap_sort_test, 15);
    printf("\n");

    return 0;
}