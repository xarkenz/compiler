module libc;

implement i32 {
    function max(self: Self, other: Self) -> Self {
        if (self > other) {
            self
        }
        else {
            other
        }
    }

    function cmp(self: *Self, other: *Self) -> i32 {
        if (*self < *other) {
            -1
        }
        else if (*self > *other) {
            +1
        }
        else {
            0
        }
    }

    function print(self: *Self) {
        libc::printf("%d", *self);
    }
}

struct LinkedListNode {
    value: *void,
    next: *mut Self,
}

struct LinkedList {
    head: *mut LinkedListNode,
}

implement LinkedList {
    function new() -> Self {
        Self {
            head: null,
        }
    }

    function front(self: *Self) -> *void {
        if (self.head == null) {
            null
        }
        else {
            self.head.value
        }
    }

    function push_front(self: *mut Self, value: *void) {
        let new_node = libc::malloc(sizeof(LinkedListNode)) as *mut LinkedListNode;
        *new_node = LinkedListNode {
            value: value,
            next: self.head,
        };
        self.head = new_node;
    }

    function pop_front(self: *mut Self) -> *void {
        if (self.head == null) {
            null
        }
        else {
            let front = self.head;
            let value = front.value;
            self.head = front.next;
            libc::free(front as *mut [u8]);
            value
        }
    }
}

struct AVLTreeNode {
    key: *void,
    left: *mut Self,
    right: *mut Self,
    height: i32,
}

implement AVLTreeNode {
    function alloc(key: *void) -> *mut Self {
        let alloc = libc::malloc(sizeof(Self)) as *mut Self;
        *alloc = Self {
            key: key,
            left: null,
            right: null,
            height: 0,
        };
        alloc
    }

    function get_height(self: *Self) -> i32 {
        // sneaky little trick
        if (self == null) {
            -1
        }
        else {
            self.height
        }
    }

    function recompute_height(self: *mut Self) {
        self.height = 1 + i32::max(
            self.left.get_height(),
            self.right.get_height(),
        );
    }

    function rotate_right(self: *mut Self) -> *mut Self {
        let new_root = self.left;
        self.left = new_root.right;
        new_root.right = self;
        self.recompute_height();
        new_root.recompute_height();
        new_root
    }

    function rotate_left(self: *mut Self) -> *mut Self {
        let new_root = self.right;
        self.right = new_root.left;
        new_root.left = self;
        self.recompute_height();
        new_root.recompute_height();
        new_root
    }

    function balance(self: *mut Self) -> *mut Self {
        if (self == null) {
            return null;
        }
        let imbalance = self.left.get_height() - self.right.get_height();
        if (imbalance > 1) {
            if (self.left.right.get_height() > self.left.left.get_height()) {
                self.left = self.left.rotate_left();
            }
            self.rotate_right()
        }
        else if (imbalance < -1) {
            if (self.right.left.get_height() > self.right.right.get_height()) {
                self.right = self.right.rotate_right();
            }
            self.rotate_left()
        }
        else {
            self.recompute_height();
            self
        }
    }

    function print(self: *Self, printer: function(*void)) {
        if (self != null) {
            libc::printf("(");
            self.left.print(printer);
            libc::printf(" ");
            printer(self.key);
            libc::printf(" ");
            self.right.print(printer);
            libc::printf(")");
        }
    }
}

struct AVLTree {
    root: *mut AVLTreeNode,
    comparator: function(*void, *void) -> i32,
}

implement AVLTree {
    function new(comparator: function(*void, *void) -> i32) -> Self {
        Self {
            root: null,
            comparator: comparator,
        }
    }

    function get(self: *Self, key: *void) -> *void {
        let mut node = self.root;
        while (node != null) {
            let ordering = (self.comparator)(key, node.key);
            if (ordering < 0) {
                node = node.left;
            }
            else if (ordering > 0) {
                node = node.right;
            }
            else {
                return node.key;
            }
        }
        null
    }

    function insert_subtree(self: *mut Self, node_ref: *mut *mut AVLTreeNode, key: *void) -> *void {
        if (*node_ref == null) {
            *node_ref = AVLTreeNode::alloc(key);
            return null;
        }
        let ordering = (self.comparator)(key, (*node_ref).key);
        if (ordering < 0) {
            self.insert_subtree(&(*node_ref).left, key);
        }
        else if (ordering > 0) {
            self.insert_subtree(&(*node_ref).right, key);
        }
        else {
            let replaced_key = (*node_ref).key;
            (*node_ref).key = key;
            return replaced_key;
        }
        *node_ref = (*node_ref).balance();
        null
    }

    function insert(self: *mut Self, key: *void) -> *void {
        self.insert_subtree(&self.root, key)
    }

    function print(self: *Self, printer: function(*void)) {
        self.root.print(printer);
        libc::printf("\n");
    }
}

struct BTreeNodeKey {
    boundary: *void,
    left: *mut BTreeNode,
}

struct BTreeNode {
    is_leaf: bool,
    degree: usize,
    keys: *mut [BTreeNodeKey],
    right: *mut Self,
}

struct BTreeLeaf {
    is_leaf: bool,
    degree: usize,
    elements: *mut [*void],
}

implement BTreeLeaf {
    function alloc(l_order: usize, first_element: *void) -> *mut Self {
        let elements = libc::malloc(sizeof(*void) * l_order) as *mut [*void];
        elements[0] = first_element;
        let alloc = libc::malloc(sizeof(Self)) as *mut Self;
        *alloc = Self {
            is_leaf: true,
            degree: 1,
            elements: elements,
        };
        alloc
    }
}

struct BTree {
    m_order: usize,
    l_order: usize,
    comparator: function(*void, *void) -> i32,
    root: *mut BTreeNode,
}

implement BTree {
    function new(m_order: usize, l_order: usize, comparator: function(*void, *void) -> i32) -> Self {
        Self {
            m_order: m_order,
            l_order: l_order,
            comparator: comparator,
            root: null,
        }
    }

    function insert(self: *mut Self, key: *void) -> *void {
        if (self.root == null) {
            self.root = BTreeLeaf::alloc(self.l_order, key) as *mut BTreeNode;
            return null;
        }
        null
    }
}

function max_percolate_down(array: *mut [*void], length: usize, comparator: function(*void, *void) -> i32, mut index: usize) {
    let target = array[index];
    while (true) {
        let left = (index + 1) * 2 - 1;
        let right = left + 1;
        if (left >= length) {
            break;
        }
        let max = if (right >= length || comparator(array[left], array[right]) > 0) {
            left
        } else {
            right
        };
        if (comparator(array[max], target) > 0) {
            array[index] = array[max];
            index = max;
        }
        else {
            break;
        }
    }
    array[index] = target;
}

function heap_sort(array: *mut [*void], length: usize, comparator: function(*void, *void) -> i32) {
    // Build max heap
    let mut index = length / 2;
    while (index > 0) {
        index -= 1;
        // Percolate down
        max_percolate_down(array, length, comparator, index);
    }
    // Build sorted array
    index = length;
    while (index > 1) {
        index -= 1;
        // Swap max with last unsorted element
        let max_value = array[0];
        array[0] = array[index];
        array[index] = max_value;
        // Percolate the root down within the unsorted range
        max_percolate_down(array, index, comparator, 0);
    }
}

function print_i32_ptr_array(array: *[*i32], length: usize) {
    if (length == 0) {
        libc::printf("[]");
        return;
    }
    libc::printf("[%d", *array[0]);
    let mut index = 1_usize;
    while (index < length) {
        libc::printf(", %d", *array[index]);
        index += 1;
    }
    libc::printf("]");
}

foreign function main() -> i32 {
    let keys: [i32; 15] = [1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15];

    let mut list = LinkedList::new();
    let mut i = 0_usize;
    while (i < 5) {
        libc::printf("push: %d\n", keys[i]);
        list.push_front(&keys[i] as *void);
        i += 1;
    }
    let mut value: *i32;
    while ((value = list.pop_front() as *i32) != null) {
        libc::printf("pop: %d\n", *value);
    }

    let mut tree = AVLTree::new(i32::cmp as function(*void, *void) -> i32);
    let mut i = 0_usize;
    while (i < 7) {
        let idx = (i * 7) % 10;
        libc::printf("insert: %d\n", keys[idx]);
        tree.insert(&keys[idx] as *void);
        i += 1;
    }
    let mut i = 0_usize;
    while (i < 10) {
        let key = tree.get(&keys[i] as *void);
        let is_contained: *[u8] = if (key != null) {
            "yes"
        } else {
            "no"
        };
        libc::printf("contains %d: %s\n", keys[i], is_contained);
        i += 1;
    }
    tree.print(i32::print as function(*void) -> void);

    let mut b_tree = BTree::new(3, 2, i32::cmp as function(*void, *void) -> i32);
    b_tree.insert(&keys[0] as *void);

    let mut heap_sort_test: [*i32; 15];
    let mut index = 0_usize;
    while (index < 15) {
        let key = &keys[((index + 7) * 7) % 15];
        heap_sort_test[index] = key;
        index += 1;
    }
    libc::printf("unsorted: ");
    print_i32_ptr_array(&heap_sort_test, 15);
    libc::printf("\n");
    heap_sort(&heap_sort_test as *mut [*void], 15, i32::cmp as function(*void, *void) -> i32);
    libc::printf("heapsort: ");
    print_i32_ptr_array(&heap_sort_test, 15);
    libc::printf("\n");

    0
}
